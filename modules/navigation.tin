#ACTION {%1 {are|is} closed.$}
{
	#regexp {%1} {{.*} {.*}} {#variable {doorName} {&2}}
}
{9}

#ACTION {^Alas, you cannot go that way...$}
{
	PLAY move/bump
}
{9}

#ACTION {^Obvious exits:$}
{
	#if {{$playerSummoner} != {}}
	{
		follow $playerSummoner;
		#variable {playerSummoner} {}
	}
}
{9}

#ACTION {^You circle %w.$}
{
	#variable {lastDirection} {%1}
}
{9}

#ACTION {^{North|South|East|West|Northwest|Northeast|Southwest|Southeast|Up|Down}: %*}
{
	#variable {%1} {%0};
	#format {lastDirection} {%n} {$lastDirection};
	#if {{$quicknav} == {true} && {%1} != {$lastDirection}}
	{
		#line {gag}
	};
	#if {{$findTerrain} != {false}}
	{
		#if {{$findTerrainDirection} == {FALSE}}
		{
			#regexp {%0} {{.*}$findTerrain} {#echo {You see $findTerrain to the %1.};SYNTH_PLAY foundTerrainSound}
		};
		#else
		{
			#regexp {%0} {^$findTerrainDirection:{.*}$findTerrain} {#echo {You see $findTerrain to the %1.};SYNTH_PLAY foundTerrainSound}
		}
	};
	#else
	{
		#if {{$playerSummoner} != {}}
		{
			follow $playerSummoner;
			#variable {playerSummoner} {}
		}
	}
}
{9}

#ALIAS {^{fin|find} {d|do|dow|down|e|ea|eas|east|n|ne|nea|neas|neast|no|nor|nort|north|northe|northea|northeas|northeast|northw|northwe|northwes|northwest|nw|nwe|nwes|nwest|s|se|sea|seas|seast|so|sou|sout|south|southe|southea|southeas|southeast|southw|southwe|southwes|southwest|sw|swe|swes|swest|u|up|w|we|wes|west} %3}
{
	#format {findTerrain} {%n} {%3};
	#variable {findTerrainDirection} {%2};
	#showme {You will now keep an eye out for ${findTerrain}s $findTerrainDirection from you.}
}
{8}

#ALIAS {^nofind$}
{
	#variable {findTerrain} {false};
	#showme {You will no longer look for any kind of terrain.}
}
{9}

#ALIAS {^{coords|coord|coordi|coordin|coordina|coordinat|coordinate|coordinates|xy}$}
{
	#showme {You are at $coordinateX, $coordinateY\.}
}
{9}

#ALIAS {^{d|do|dow|down|e|ea|eas|east|n|ne|nea|neas|neast|no|nor|nort|north|northe|northea|northeas|northeast|northw|northwe|northwes|northwest|nw|nwe|nwes|nwest|s|se|sea|seas|seast|so|sou|sout|south|southe|southea|southeas|southeast|southw|southwe|southwes|southwest|sw|swe|swes|swest|u|up|w|we|wes|west}$}
{
	#switch {{%0}}
	{
		#case {{{d|do|dow}}}
		{
			#variable {lastDirection} {Down}
		};
		#case {{{e|ea|eas}}}
		{
			#variable {lastDirection} {East}
		};
		#case {{{n|no|nor|nort}}}
		{
			#variable {lastDirection} {North}
		};
		#case {{{ne|northe|northea|northeas}}}
		{
			#variable {lastDirection} {Northeast}
		};
		#case {{{nw|northw|northwe|northwes}}}
		{
			#variable {lastDirection} {Northwest}
		};
		#case {{{s|so|sou|sout}}}
		{
			#variable {lastDirection} {South}
		};
		#case {{{se|southe|southea|southeas}}}
		{
			#variable {lastDirection} {Southeast}
		};
		#case {{{sw|southw|southwe|southwes}}}
		{
			#variable {lastDirection} {Southwest}
		};
		#case {{u}}
		{
			#variable {lastDirection} {Up}
		};
		#case {{{w|we|wes}}}
		{
			#variable {lastDirection} {West}
		};
		#default
		{
			#variable {lastDirection} {%0}
		}
	};
	#if {{$autosneak} == {true}}
	{
		#send {sneak %0}
	};
	#else
	{
		#send {%0}
	};
	#if {{$autoclaim} == {true}}
	{
		claim
	};
	#variable {reverse} {@get_reverse{$lastDirection}}
}
{9}

#ALIAS {^{fin|find} %2}
{
	#format {findTerrain} {%n} {%2};
	#variable {findTerrainDirection} {FALSE};
	#showme {You will now keep an eye out for ${findTerrain}s.}
}
{9}

#ALIAS {^{l|lo|loo|look} all$}
{
	#local {i} {};
	#foreach {north;south;east;west;northwest;northeast;southwest;southeast} {i}
	{
		#showme {$i};
		look $i
	}
}
{9}

#ALIAS {^{noauto|auto}{claim|sneak}$}
{
	#if {{%0} == {auto{claim|sneak}}}
	{
		#showme {Auto %2 enabled.};
		#variable {auto%2} {true}
	};
	#else
	{
		#showme {Auto %2 disabled.};
		#unvariable {auto%2}
	}
}
{9}

#ALIAS {^{noquick|quick}nav$}
{
	#if {{%0} == {quicknav}}
	{
		#variable {quicknav} {true};
		#showme {You will only see the exits in the same direction you are traveling.}
	};
	#else
	{
		#unvariable {quicknav};
		#showme {Exit information will be displayed normally.}
	}
}
{9}

#ALIAS {^{sca|scan}$}
{
	#echo {$North\n $East\n $South\n $West\n $Northwest\n $Northeast\n $Southwest\n $Southeast}
}
{9}

#FUNCTION {get_reverse}
{
	#switch {{$lastDirection}}
	{
		#case {{down}}
		{
			#variable {result} {up}
		};
		#case {{east}}
		{
			#variable {result} {west}
		};
		#case {{north}}
		{
			#variable {result} {south}
		};
		#case {{northeast}}
		{
			#variable {result} {southwest}
		};
		#case {{northwest}}
		{
			#variable {result} {southeast}
		};
		#case {{south}}
		{
			#variable {result} {north}
		};
		#case {{southeast}}
		{
			#variable {result} {northwest}
		};
		#case {{southwest}}
		{
			#variable {result} {northeast}
		};
		#case {{west}}
		{
			#variable {result} {east}
		};
		#case {{up}}
		{
			#variable {result} {down}
		}
	};
	#return $result
}

#MACRO {\e,}
{
	south
}

#MACRO {\e.}
{
	southeast
}

#MACRO {\e<}
{
	down
}

#MACRO {\eI}
{
	up
}

#MACRO {\eJ}
{
	#if {{$pathLoaded} == {true}}
	{
		#path {walk} {backward}
	};
	#else
	{
		#showme {Path not loaded.}
	}
}

#MACRO {\eK}
{
	enter portal
}

#MACRO {\ei}
{
	north
}

#MACRO {\ej}
{
	west
}

#MACRO {\ek}
{
	scan
}

#MACRO {\el}
{
	east
}

#MACRO {\em}
{
	southwest
}

#MACRO {\eo}
{
	northeast
}

#MACRO {\eu}
{
	northwest
}

#VARIABLE         {Down}  {}
#VARIABLE         {East}  {}
#VARIABLE         {North}  {}
#VARIABLE         {Northeast}  {}
#VARIABLE         {Northwest}  {}
#VARIABLE         {South}  {}
#VARIABLE         {Southeast}  {}
#VARIABLE         {Southwest}  {}
#VARIABLE         {Up}  {}
#VARIABLE         {West}  {}
#VARIABLE         {coordinateX}  {}
#VARIABLE         {coordinateY}  {}
#VARIABLE         {doorName}  {}
#VARIABLE         {findTerrain}  {false}
#VARIABLE         {findTerrainDirection}  {FALSE}
#VARIABLE         {lastDirection}  {}
#VARIABLE         {listIndex}  {}
#VARIABLE         {location}  {}
#VARIABLE         {reverse}  {}
