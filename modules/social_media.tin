#action {^You finish excavating the trench!$}
{
post $name finished excavating a trench!
}
{9}

#action {^Your trading post item '%1' has sold for %d %3 coins.$}
{
post $name sold %1 for %2 %3 coins on the trading post.
}
{9}

#action {^You have been enrolled in %1.$}
{
#variable {tmp} {%1};
#replace {tmp} { } {};
#variable {empireName} {%1};
#if {"$mp" == "%1"}
{
post $name is now a member of %1!
};
#else
{
post $name is now a member of #%1!
}
}
{9}

#action {^[ {A|An} {alliance|trade agreement} has been established with %3! ]$}
{
#if {&{empireName}}
{
post $empireName established %1 %2 with %3!
}
}
{9}

#action {^You discover that this area is {a|a deep} {copper|gold|imperium|iron|nocturnium|silver} mine{\.|!}$}
{
play skills/prospect_%2;
#math {mineCounter.%2} {${mineCounter.%2} + 1};
#math {mineCounter.total} {${mineCounter.total} + 1}
}
{9}

#action {^You improve your {Battle|Empire|High Sorcery|Natural Magic|Stealth|Survival|Trade|Vampire} skill to 100.$}
{
post $name improved %1 to 100!
}
{9}

#action {^You have specialized in {Battle|Empire|High Sorcery|Natural Magic|Stealth|Survival|Trade|Vampire}.}
{
post $name has specialized in %1.
}
{9}

#ALIAS {^post %1}
{
	#if {"$pump" == "true"}
	{
		#system {p post note --to followers --cc public --title "Alter Aeon - http://alteraeon.com" "%1" &> /dev/null&}
	};
	#if {"$twitter" == "true"}
	{
		#system {ttytter -silent -norc -status="%1" &> /dev/null&}
	};
	#if {"$gnusocial" == "true"}
	{
#if {!&{gusername} || !&{gpassword}}
{
#showme {Before you can post, you need to set your username and password. To do so please type ginfo username@node.ext password.};
#break
};
#if {!&{gurl}}
{
#variable {gurl} {https://2mb.social}
};
#if {!&{gnuSocialMudGroup}}
{
#variable {gnuSocialMudGroup} {muds}
};
		#system {curl -s -u "$gusername:$gpassword" -d status="$(echo -e "%1") #EmpireMUD !$gnuSocialMudGroup" -d source="tintin-empiremud" $gurl/api/statuses/update.xml &> /dev/null&}
	}
}
{9}

#ALIAS {^{gnusocial|pump.io|twitter|nofacebook|nognusocial|nopump.io|notwitter}$}
{
#switch {"%0"}
{
#case {"gnusocial"}
{
	#variable {gnusocial} {true};
	#showme {Achievements will now be posted to GNU Social. Remember to set your webfinger and password with ginfo webfinger password.}
};
#case {"nognusocial"}
{
	#unvariable {gnusocial};
	#showme {Achievements will no longer be posted to GNU Social.}
};
#case {"pump.io"}
{
	#variable {pump} {true};
	#showme {Achievements will now be posted to Pump.io.}
};
#case {"nopump.io"}
{
	#unvariable {pump};
	#showme {Achievements will no longer be posted to Pump.io.}
};
#case {"twitter"}
{
	#variable {twitter} {true};
	#showme {Achievements will now be posted to Twitter.}
};
#case {"notwitter"}
{
	#unvariable {twitter};
	#showme {Achievements will no longer be posted to Twitter.}
};
}
}
{9}

#alias {^ginfo %S@%S %s%4$}
{
#variable {gusername} {%1};
#variable {gurl} {%2};
#variable {gpassword} {%4};
#showme {Now posting to GNU Social as %1@%2.}
}
{9}
#variable {mineCounter.copper} {0}
#variable {mineCounter.gold} {0}
#variable {mineCounter.imperium} {0}
#variable {mineCounter.iron} {0}
#variable {mineCounter.nocturnium} {0}
#variable {mineCounter.silver} {0}
#variable {mineCounter.total} {0}
