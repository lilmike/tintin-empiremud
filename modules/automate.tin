#ACTION {^You are hungry.$}
{
	PLAY health/hungry;
	#if {"$autoforage" == "true"}
	{
		forage
	}
}
{8}

#ACTION {^You forage around %*}
{
	PAN_PLAY skills/search;
	#if {"$autoforage" == "true"}
	{
		#regexp {%1} {and find {a|an|some} {bunch of|couple red|dry|few|green|handful of|juicy|plump|red|wood ear} {.*}(x{.*})} {#&4 eat &3}
	}
}
{8}

#ACTION {^You lunge at %*, grasping onto its neck and biting into it vigorously!$}
{
	#variable {feeding} {true}
}
{8}

#ACTION {%* is {stunned, but will probably regain consciousness again|knocked unconscious|incapacitated and will slowly die, if not aided}{\.|!}$}
{
	PLAY combat/unconscious;
	#if {"$autobite" == "true" && "$feeding" == "false"}
	{
		#regexp {%1} {{.*} {.*}} {bite &2};
		#variable {feeding} {true}
	}
}
{9}

#ACTION {%1 is trying to summon you to %2 -- use 'accept/reject summon'.$}
{
	#variable {playerSummoner} {%1}
}
{9}

#ACTION {^No-one by that name here.$}
{
	#variable {feeding} {false}
}
{9}

#ACTION {^You pull the last of %* blood from {her|his|its} veins, and {he|it|she} falls limply to the ground!$}
{
	#variable {feeding} {false};
	PLAY combat/vampire_feed
}
{9}

#ACTION {^You stop feeding.$}
{
	#variable {feeding} {false}
}
{9}

#ACTION {^Your counterspell {goes off!|expires\.}$}
{
	#list {autoDo} {find} {counterspell} {i};
	#if {$i > 0}
	{
		counterspell
	}
}
{9}

#ACTION {^[ The mud will {shutdown|reboot} in %d minut{e|es} (type 'confirm' to {reboot|shutdown} faster) ]$}
{
	PLAY system/%1 system/%2-minute;
	#if {"$autoconfirmreboots" == "true" && "$rebootConfirmed" != "true"}
	{
		#variable {rebootConfirmed} {true};
		confirm
	};
	#if {%2 == 1}
	{
		#variable {rebootConfirmed} {false}
	}
}
{8}

#ALIAS {^autodo {add|remove} %w$}
{
	#if {"%1" == "add"}
	{
		#list {autoDo} {find} {%2} {i};
		#if {$i == 0}
		{
			#list {autoDo} {add} {%2};
			#echo {%2 added to auto do list}
		};
		#else
		{
			#echo {%2 already in list}
		}
	};
	#else
	{
		#list {autoDo} {find} {%2} {i};
		#if {$i > 0}
		{
			#list {autoDo} {delete} {$i};
			#echo {%2 removed from auto do list}
		};
		#else
		{
			#echo {%2 not found.}
		}
	}
}
{9}

#ALIAS {^autodo$}
{
	#forall {${autoDo[]}}{#echo {${autoDo[&0]}}}
}
{9}

#ALIAS {^autoshield %1}
{
	#showme {You will now equip %1 as shield in melee combat.};
	#variable {autoshield} {%1}
}
{9}

#ALIAS {^noautodo$}
{
	#echo {auto do list cleared};
	#unvariable {autoDo}
}
{9}

#ALIAS {^noautoshield$}
{
	#showme {Autoshield disabled.};
	#unvariable {autoshield}
}
{9}

#ALIAS {^{noauto|auto}bite$}
{
	#if {"%0" == "autobite"}
	{
		#showme {autobite enabled};
		#variable {autobite} {true}
	};
	#else
	{
		#showme {autobite disabled};
		#unvariable {autobite}
	}
}
{9}

#ALIAS {^{noauto|auto}confirmreboots$}
{
	#if {"%0" == "autoconfirmreboots"}
	{
		#showme {You will now automatically confirm reboots when asked by the game.};
		#variable {autoconfirmreboots} {true}
	};
	#else
	{
		#showme {Autoconfirmreboots disabled.};
		#unvariable {autoconfirmreboots}
	}
}
{9}

#ALIAS {^{noauto|auto}forage$}
{
	#if {"%0" == "autoforage"}
	{
		#echo {You will automatically forage for and eat food when you are hungry.};
		#variable {autoforage} {true}
	};
	#else
	{
		#showme {Autoforage disabled.};
		#unvariable {autoforage}
	}
}
{9}

#ALIAS {^{noauto|auto}{loot|skin}$}
{
	#switch {"%0"}
	{
		#case {"autoloot"}
		{
			#showme {autoloot is enabled.};
			#variable {autoloot} {true}
		};
		#case {"noautoloot"}
		{
			#showme {autoloot is disabled.};
			#variable {autoloot} {false}
		};
		#case {"autoskin"}
		{
			#showme {autoskinning is enabled.};
			#variable {autoskin} {true}
		};
		#case {"noautoskin"}
		{
			#showme {autoskinning is disabled.};
			#variable {autoskin} {false}
		}
	}
}
{9}

#VARIABLE         {autoloot}  {false}
#VARIABLE         {autoskin}  {false}
#VARIABLE         {feeding}  {false}
#VARIABLE         {lastCommand}  {}
#VARIABLE         {playerSummoner}  {}
#VARIABLE         {rebootConfirmed}  {false}
