#ALIAS {^Tile_Play}
{
	#if {!&{MSP[VOLUME]}}
	{
		#variable {MSP[VOLUME]} {0}
	};
	#if {{$bgm} == {true}}
	{
		#if {{${oldSoundFile}} != {${tileSoundFile}}}
		{
			#if {{$tileSoundFilePID} != {false}}
			{
				#system {kill $tileSoundFilePID;echo "false" > .tileSoundFilePID};
				#variable {tileSoundFilePID} {false}
			};
			#variable {oldSoundFile} {$tileSoundFile};
			#script {bash -c 'if [ -f "sounds/terrain/$tileSoundFile" ] ; then play -qV0 "sounds/terrain/$tileSoundFile" repeat - gain ${MSP[VOLUME]} &> /dev/null&echo "$!" > .tileSoundFilePID;echo "#variable {tileSoundFilePID} {$(cat .tileSoundFilePID)}";else echo "false" > .tileSoundFilePID;echo "#variable {tileSoundFilePID} {$(cat .tileSoundFilePID)}";fi'}
		}
	}
}
{9}

#ALIAS {^{nobgm|bgm}$}
{
	#if {{%0} == {bgm}}
	{
		#variable {bgm} {true};
		#showme {Tile ambiance enabled.};
		Tile_Play tileSoundFile
	};
	#else
	{
		#if {{$tileSoundFilePID} != {false}}
		{
			#system {kill $tileSoundFilePID;echo "false" >.tileSoundFilePID };
			#variable {tileSoundFilePID} {false}
		};
		#unvariable {bgm};
		#variable {oldSoundFile} {};
		#showme {Tile ambiance disabled.}
	}
}
{9}

#EVENT {VARIABLE UPDATE MSDP_ROOM}
{
	#if {!$copying}
	{
		#variable {tmp} {%1};
		#if {{${tmp[NAME]}} != {{$tileNames}}}
		{
			#variable {tileSoundFile} {none}
		};
		#else
		{
			#if {{${tmp[NAME]}} == {A Field of {.*}}}
			{
				#variable {tileSoundFile} {field}
			};
			#elseif {{${tmp[NAME]}} == {%*Forest}}
			{
				#variable {tileSoundFile} {forest}
			};
			#elseif {{${tmp[NAME]}} == {%*Orchard}}
			{
				#variable {tileSoundFile} {orchard}
			};
			#else
			{
				#format {tileSoundFile} {%l} {${tmp[NAME]}};
				#replace {tileSoundFile} {'} {};
				#replace {tileSoundFile} { } {_}
			}
		};
		#if {{${tmp[COORDS][X]},${tmp[COORDS][Y]}} != {$oldCoordinates}}
		{
			#if {{$tileSoundFile} == {none}}
			{
				Tile_Play $tileSoundFile;
				PLAY move/default_step
			};
			#else
			{
				PLAY move/$tileSoundFile;
				#variable {tileSoundFile} {$tileSoundFile\.opus};
				#replace {tileSoundFile} {\\} {};
				Tile_Play $tileSoundFile
			};
			#variable {oldCoordinates} {${tmp[COORDS][X]},${tmp[COORDS][Y]}}
		};
		#variable {coordinateX} {${tmp[COORDS][X]}};
		#variable {coordinateY} {${tmp[COORDS][Y]}}
	}
}

#VARIABLE         {oldCoordinates}  {}
#VARIABLE         {oldSoundFile}  {}
#VARIABLE         {tileNames}  {A Bridge Over the River|A Carrot Patch|A Field of .*|(A|An) .*Forest|A Foundry|A Garden Made of Rafts|A Hot Forge|A Lake|A Large Tomb|A Mine Shaft|A Murky Swamp|A Patch of Ripe Blackberries|A Potter's Hut|A Rice Paddy|A River|A Seeded Field|A Shady Garden|A Terraced Garden|(A|An) .* Orchard|An Estuary Garden|The City Center|The Open Plains|On a Mountain|On a Road|The Carpenter|The Docks|The Glass Blower|The Jungle|The Ocean}
#VARIABLE         {tileSoundFile}  {}
#VARIABLE         {tileSoundFilePID}  {false}
#VARIABLE         {tmp}  {}
